
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        if(!project.hasProperty("bootstrap")) {
            classpath group: "me.seeber.gradle", name: "gradle-bintray-config", version: "1.1.2"
            classpath group: "me.seeber.gradle", name: "gradle-github-config", version: "1.1.2"
            classpath group: "me.seeber.gradle", name: "gradle-license-config", version: "1.1.2"
            classpath group: "me.seeber.gradle", name: "gradle-plugin-publish-config", version: "1.1.2"
            classpath group: "me.seeber.gradle", name: "gradle-project-config", version: "1.1.2"
        }
    }
}

version = "1.1.2"
group = "me.seeber.gradle"
description = "Default configuration for the license plugin"

if(project.hasProperty("bootstrap")) {
    apply plugin: "java"
    apply plugin: "maven"

    dependencies {
        compile(gradleApi())
        compile(localGroovy())

        compileOnly(group: "org.eclipse.jdt", name: "org.eclipse.jdt.annotation", version: "2.0.0")

        testCompile(gradleTestKit())

        testCompile(group:"org.spockframework", name: "spock-core", version: "1.0-groovy-2.4") {
            exclude(group: "org.codehaus.groovy", module: "groovy-all")
        }
    }
}
else {
    apply plugin: "me.seeber.distribution.bintray"
    apply plugin: "me.seeber.distribution.gradle-plugin"
    apply plugin: "me.seeber.distribution.maven"
    apply plugin: "me.seeber.ide.eclipse"
    apply plugin: "me.seeber.project.gradle-plugin"
    apply plugin: "me.seeber.repository.github"
    apply plugin: "me.seeber.setup.license"
    apply plugin: "me.seeber.validation.checkstyle"

    model {
        projectConfig {
            organization.name = "Jochen Seeber"
            inceptionYear = 2016
            license.id = "BSD 2-Clause"
        }

        githubConfig { user = "jochenseeber" }
    }

    pluginBundle {
        tags = ["license", "configuration"]

        plugins { licensePlugin.id = "me.seeber.setup.license"  }
    }
}

dependencies {
    compile(group: "me.seeber.gradle", name: "gradle-project-config", version: "1.1.2")
    compile(group: "gradle.plugin.nl.javadude.gradle.plugins", name: "license-gradle-plugin", version: "0.13.1") { exclude group: "com.android.tools.build" }

    if(project.hasProperty("bootstrap")) {
        compile(gradleApi())
        compile(localGroovy())

        compileOnly(group: "org.eclipse.jdt", name: "org.eclipse.jdt.annotation", version: "2.0.0")
    }
    else {
        annotations(group: "me.seeber.annotations", name: "jdk-annotations", version: "1.8.0-r.4")
        annotations(group: "me.seeber.annotations", name: "gradle-api-annotations", version: "3.1-r.4")
    }
}

if(project.hasProperty("bootstrap")) {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "https://plugins.gradle.org/m2" }
        jcenter()
    }
}
